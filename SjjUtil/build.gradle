apply plugin: 'com.android.application'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-android'
apply plugin: 'com.jakewharton.butterknife'

def releaseTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"))
}

android {
    compileSdkVersion 27
    buildToolsVersion "27.0.0"
    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 23
        // dex突破65535的限制
        multiDexEnabled true
        // 默认是umeng的渠道
        manifestPlaceholders = [UMENG_CHANNEL_VALUE: "umeng"]

        buildConfigField "String", "GIT_REVISION", "\"${getGitVersion()}\""
        buildConfigField "int", "BUILD_NO", "${getJenkinsBuild()}"
    }
    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }

        // Move the tests to tests/java, tests/res, etc...
        instrumentTest.setRoot('tests')

        // Move the build types to build-types/<type>
        // For instance, build-types/debug/java, build-types/debug/AndroidManifest.xml, ...
        // This moves them out of them default location under src/<type>/... which would
        // conflict with src/ being used by the main source set.
        // Adding new build types or product flavors should be accompanied
        // by a similar customization.
        debug.setRoot('build-types/debug')
        release.setRoot('build-types/release')
    }

    // 移除lint检查的error
    lintOptions {
        abortOnError false
    }

    signingConfigs {
        debug {
            storeFile file("${rootDir}/file/key/debug.keystore")
        }

        release {
            storeFile file("${rootDir}/file/key/debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
    }

    buildTypes {
        debug {
            // 显示Log
            buildConfigField "boolean", "MY_LOG_DEBUG", "true"

            versionNameSuffix "-debug"
            minifyEnabled false
            zipAlignEnabled false
            shrinkResources false
            signingConfig signingConfigs.debug
        }

        release {
            // 不显示Log
            buildConfigField "boolean", "LOG_DEBUG", "false"

            minifyEnabled true
            zipAlignEnabled true
            // 移除无用的resource文件
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    flavorDimensions "tier"

    // 友盟多渠道打包
    productFlavors {
        wandoujia {
            dimension "tier"
        }
//        _360 {}
//        baidu {}
//        xiaomi {}
//        tencent {}
//        taobao {}
    }

    productFlavors.all { flavor ->
        flavor.manifestPlaceholders = [UMENG_CHANNEL_VALUE: name]
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
    }

    compileOptions {
        sourceCompatibility JAVA_VERSION
        targetCompatibility JAVA_VERSION
    }
}

dependencies {
    api fileTree(dir: 'libs', include: '*.jar')

//    provided files('lib/layoutlib-15.jar')

    api 'com.android.support:appcompat-v7:27.0.0'
    api 'com.android.support:recyclerview-v7:27.0.0'

    api 'com.github.bumptech.glide:glide:3.7.0'
    api 'com.mcxiaoke.volley:library:1.0.19'

    api 'io.reactivex.rxjava2:rxjava:2.1.0'
    api 'io.reactivex.rxjava2:rxandroid:2.0.1'
    api 'com.jakewharton.rxbinding2:rxbinding:2.0.0'

    api 'com.jakewharton:butterknife:8.5.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.5.1'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

def getGitVersion() {
    return 'git rev-parse --short HEAD'.execute().text.trim()
}

def getJenkinsBuild() {
    return System.getenv("BUILD_NUMBER") as Integer ?: 0
}

//Gradle 3.0.0以后不能用了£
//applicationVariants.all { variant ->
//    variant.outputs.each { output ->
//        def outputFile = output.outputFile
//        if (outputFile != null && outputFile.name.endsWith('.apk')) {
//            // 输出apk名称为boohee_v1.0_2015-01-15_wandoujia.apk
//            def fileName = "boohee_v${defaultConfig.versionName}_${releaseTime()}_${variant.productFlavors[0].name}.apk"
//            output.outputFile = new File(outputFile.parent, fileName)
//        }
//    }
//}

//task to copy library to libs
//task copyLibrary(type: Copy) {
//    println "============= copyLibrary"
//    from('build/outputs/apk/wandoujia/debug/SjjUtil-wandoujia-debug.apk')
//    into('build/outputs/apk')
/////Rename the aar
//    rename('SjjUtil-wandoujia-debug.apk', 'SjjUtil-123.apk')
//}
//copyLibrary.dependsOn(build)